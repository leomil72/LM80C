10 REM
20 REM             DAMA
30 REM GIOCO ORIGINARIAMENTE PUBBLICATO
40 REM DA SYSTEMS EDITORIALE PER C16/+4
50 REM CONVERTITO PER LM80C COLOR COMPUTER
60 REM DA LEONARDO MILIANI
70 REM
100 GOSUB 8000
110 GOSUB 10000
190 RV=1/3:DIM TC(4),R(4),SC(7,7),CC(27),FC(27):G=-1:TC(2)=SP
200 REM
210 REM *** DISEGNA LA DAMIERA ***
220 REM
230 CLS
240 CH=160:FOR Y=0 TO 21 STEP 3:FOR X=1 TO 22 STEP 3:FOR I=0 TO 2
250 LOCATE X,Y+I:PRINT CHR$(CH);CHR$(CH);CHR$(CH);
260 NEXT:IF CH=160 THEN CH=161:GOTO 280
270 CH=160
280 NEXT:IF CH=161 THEN CH=160:GOTO 286
283 CH=161
286 NEXT
290 FOR I=0 TO 7:LOCATE 0,1+(I*3):PRINT CHR$(65+I);:NEXT
295 FOR I=0 TO 7:LOCATE 2+(I*3),0:PRINT CHR$(49+I);:NEXT
300 MO=0:R(0)=-99:PC=12:PT=12:IS=6144
305 REM *** STATO INIZIALE DELLE PEDINE ***
310 FOR X=0 TO 7:FOR Y=0 TO 7:IF (-1)^(X+Y)>0 THEN 350
320 IF Y<3 THEN SC(X,Y)=1:GOTO 350
330 IF Y>4 THEN SC(X,Y)=-1:GOTO 350
340 SC(X,Y)=0
350 NEXT:NEXT
360 TC(0)=192:TC(1)=193:TC(3)=163:TC(4)=162
365 REM TC(0)=PEDINA NERA;TC(1)=DAMA NERA;TC(2)=SPAZIO
366 REM TC(3)=PEDINA BIANCA;TC(4)=DAMA BIANCA
370 GOSUB 1760:YC=2:PX=25:GOTO 1290
380 FOR X=0 TO 7:FOR Y=0 TO 7:IF SC(X,Y)>-1 THEN 410
390 IF SC(X,Y)=-1 THEN FOR A=-1 TO 1 STEP 2:B=G:MA=0:MC=0:GOSUB 420:NEXT
400 IF SC(X,Y)=-2 THEN 407
403 GOTO 410
407 FOR A=-1 TO 1 STEP 2:FOR B=-1 TO 1 STEP 2:MA=0:MC=0:GOSUB 420:NEXT B,A
410 NEXT Y,X:GOSUB 1880:GOTO 1100
420 U=X+A:V=Y+B:IF U<0 OR U>7 OR V<0 OR V>7 THEN 480
430 IF SC(U,V)<0 THEN 480
440 IF SC(X,Y)=-1 AND SC(U,V)=2 THEN 480
450 IF SC(U,V)=0 THEN GOSUB 490:GOTO 480
460 U=U+A:V=V+B:IF U<0 OR U>7 OR V<0 OR V>7 THEN 480
470 IF SC(U,V)=0 THEN PN=1:GOSUB 490
480 RETURN
490 REM
500 IF ABS(Y-V)=2 THEN Q=99:MA=1
510 IF MA=1 AND SC(X,Y)=-2 THEN Q=Q+50
520 IF MA=0 THEN 600
530 UU=(X+U)/2:VV=(Y+V)/2
540 IF SC(X,Y)=-2 THEN Q=Q+50
550 X1=4*UU-3*X:Y1=4*VV-3*Y:IF X1<0 OR X1>7 OR Y1<0 OR Y1>7 THEN 570
560 IF SC(3*UU-2*X,3*VV-2*Y)=1 AND SC(X1,Y1)=0 THEN Q=Q+10:GOTO 1060
570 IF Y1<0 OR Y1>7 THEN 1060
580 IF SC(UU,3*VV-2*Y)=1 AND SC(X,Y1)=0 THEN Q=Q+10
590 GOTO 1060
600 FOR C=-1 TO 1 STEP 2:IF U+C<0 OR U+C>7 OR V+G<0 THEN 700
610 IF U-C<0 OR U-C>7 OR V-G>7 THEN 700
620 IF SC(U+C,V+G)>0 AND (SC(U-C,V-G)=0 OR (U-C=X AND V-G=Y)) THEN Q=Q-45:MC=1
630 IF MC=0 THEN 700
640 MC=0:X1=X-2:Y1=Y+2:IF X1<0 OR X1>7 OR Y1<0 OR Y1>7 THEN 670
650 IF SC(X-1,Y+1)=-1 AND SC(X1,Y1)=0 THEN TP=1:Q=-97
660 IF TP=1 AND Y1=7 THEN Q=-98:TP=0:MC=0:GOTO 700
670 X1=X+2:IF X1<0 OR X1>7 OR Y1<0 OR Y1>7 THEN 700
680 IF SC(X+1,Y+1)=-1 AND SC(X1,Y1)=0 THEN TP=1:Q=-97
690 IF TP=1 AND Y1=7 THEN Q=-98:TP=0
700 NEXT C:GOSUB 2260:IF SC(X,Y)=-2 THEN GOSUB 1960:GOTO 1060
710 IF Y=1 THEN Q=Q+15
720 IF FC(10)=0 THEN 800
730 IF Y<4 AND (U=0 OR U=7) AND CC(10)=0 THEN Q=Q+15
740 IF FC(18)=0 THEN 800
750 IF CC(18)>0 THEN 1060
760 IF Y<4 AND CC(10)=0 AND CC(17)<>2 AND CC(18)=0 THEN Q=Q+15
770 IF CC(10)<>0 AND CC(17)<>0 AND CC(18)=0 THEN SW=1:Q=Q+5
780 IF SW=1 AND Y<3 THEN Q=Q+10:SW=0
790 IF (CC(10)=0 AND CC(17)=2) OR (CC(10)=2 AND CC(17)=0) THEN Q=Q-30
800 IF FC(2)=0 OR FC(16)=0 THEN 830
810 IF CC(2)>0 AND CC(5)=-1 AND CC(12)=-1 AND CC(16)=0 THEN Q=Q-30
820 IF CC(16)=2 AND CC(12)<0 AND CC(5)<0 AND CC(2)=0 THEN Q=Q-40
830 IF FC(6)=0 OR FC(17)=0 THEN 850
840 IF CC(17)<>0 AND CC(10)>0 AND CC(6)=0 THEN Q=Q+5
850 IF FC(6)=0 THEN 880
860 IF CC(6)=0 AND CC(10)=1 AND (U=7 OR U=0) THEN Q=Q+5
870 IF CC(6)>0 AND CC(10)<0 THEN Q=Q+35
880 IF FC(22)=0 THEN 900
890 IF CC(22)>0 AND CC(18)=-1 THEN Q=Q+35
900 IF FC(5)=0 OR FC(2)=0 THEN 920
910 IF CC(5)=-1 AND CC(2)>0 THEN Q=Q-30
920 IF FC(5)=0 THEN 940
930 IF Y=7 AND CC(5)=1 THEN Q=Q-20
940 IF Y=7 AND PC>7 THEN Q=Q-8
950 IF Y=6 AND PC>8 THEN Q=Q-7
960 IF FC(4)=0 THEN 980
970 IF CC(4)=2 THEN Q=Q+40
980 IF FC(12)=0 OR FC(5)=0 THEN 1000
990 IF CC(12)=2 AND CC(5)=0 THEN Q=Q+40
1000 IF FC(5)=0 OR FC(12)=0 THEN 1020
1010 IF CC(5)>0 AND CC(12)=0 THEN Q=Q+40
1020 IF FC(14)=0 OR FC(18)=0 THEN 1040
1030 IF CC(10)=-1 AND CC(18)=-1 AND CC(14)=-1 THEN Q=Q+10
1040 IF FC(21)=0 THEN 1060
1050 IF CC(17)=-1 AND CC(18)=-1 AND CC(21)=-1 THEN Q=Q+10
1060 LOCATE PX,2:IF PN=1 THEN PN=0:C$=" Penso ":GOTO 1070
1065 PN=1:C$="       "
1070 PRINT C$;:GOTO 1080
1080 IF Q>R(0) THEN R(0)=Q:R(1)=X:R(2)=Y:R(3)=U:R(4)=V
1090 Q=0:RETURN
1100 IF R(0)=-99 THEN 1580
1110 MO=MO+1:LOCATE PX,0:PRINT "M.:";MO;
1120 LOCATE PX,2:PRINT "CPU   ";:GOSUB 1740
1125 LOCATE PX,3:PRINT "Da  ";C$
1130 GOSUB 1750:YC=4:LOCATE PX,YC:PRINT "In  ";C$
1140 R(0)=-99
1150 IF R(4)=0 THEN SC(R(3),R(4))=-2:GOTO 1170
1160 SC(R(3),R(4))=SC(R(1),R(2))
1170 SC(R(1),R(2))=0:IF ABS(R(1)-R(3))<>2 THEN 1280
1180 SC((R(1)+R(3))/2,(R(2)+R(4))/2)=0
1190 X=R(3):Y=R(4):IF SC(X,Y)=-1 THEN B=-2:FOR A=-2 TO 2 STEP 4:GOSUB 1240
1200 IF SC(X,Y)=-2 THEN FOR A=-2 TO 2 STEP 4:FOR B=-2 TO 2 STEP 4:GOSUB 1240:NEXT B
1210 NEXT A
1220 IF R(0)<>-99 THEN GOSUB 1750:GOSUB 1235:GOTO 1150
1230 PRINT:GOTO 1280
1235 YC=YC+1:LOCATE PX,YC:PRINT "In  ";C$:R(0)=-99:RETURN
1240 U=X+A:V=Y+B:IF U<0 OR U>7 OR V<0 OR V>7 THEN 1270
1250 IF SC(X,Y)=-1 AND SC(U,V)=2 THEN R(0)=-99:GOTO 1270
1260 IF SC(U,V)=0 AND SC(X+A/2,Y+B/2)>0 THEN GOSUB 490
1270 RETURN
1280 GOSUB 1760
1282 REM
1285 REM *** MOSSA GIOCATORE ***
1287 REM
1290 YC=YC+2:LOCATE PX,YC:PRINT "Tu";:YC=YC+1
1300 LOCATE PX,YC:INPUT "Da";A$:GOSUB 1600:X=E:Y=H
1310 IF X<0 OR Y<0 THEN LOCATE PX,YC:PRINT SPC(4);:GOTO 1300
1320 IF SC(X,Y)<=0 THEN LOCATE PX,YC:PRINT SPC(4);:GOTO 1300
1330 IF WQ=1 THEN WQ=0:LOCATE PX,YC:PRINT SPC(4);:GOTO 1300
1340 YC=YC+1:LOCATE PX,YC:INPUT "In";A$:GOSUB 1700:X=A:Y=B
1350 IF WQ=1 THEN WQ=0:LOCATE PX,YC:PRINT SPC(4);:GOTO 1340
1360 QW=SC((E+A)/2,(H+B)/2)
1370 GOSUB 1650
1373 IF WQ=1 AND QW>=0 AND ABS(B-H)<=2 THEN 1378
1375 GOTO 1380
1378 WQ=0:LOCATE PX,YC:PRINT SPC(4);:YC=YC-1:GOTO 1300
1380 IF B-H<0 AND SC(E,H)=1 THEN 1430
1390 IF ABS(B-H)=2 AND QW>=0 THEN 1430
1400 IF ABS(B-H)=2 AND QW=-2 AND SC(E,H)=1 THEN 1430
1410 IF SC(X,Y)<>0 OR ABS(A-E)>2 OR ABS(A-E)<>ABS(B-H) THEN 1430
1420 GOTO 1440
1430 YC=YC+1:LOCATE PX,YC:GOTO 1340
1440 SC(A,B)=SC(E,H):SC(E,H)=0:IF ABS(E-A)<>2 THEN 1520
1450 SC((E+A)/2,(H+B)/2)=0:GOSUB 1760
1460 FOR J=-1 TO 1 STEP 2:FOR K=-1 TO 1 STEP 2:X2=A+2*K:Y2=B+2*J
1470 IF X2<0 OR X2>7 OR Y2<0 OR Y2>7 THEN 1510
1480 X1=A+K:Y1=B+J:IF X1<0 OR X1>7 OR Y1<0 OR Y1>7 THEN 1510
1490 IF SC(A,B)=1 AND SC(X2,Y2)=0 AND SC(X1,Y1)=-1 AND J=1 THEN 1550
1500 IF SC(A,B)=2 AND SC(X2,Y2)=0 AND SC(X1,Y1)<0 THEN 1550
1510 NEXT:NEXT
1520 GOTO 1560
1530 REM
1540 REM
1550 E=A:H=B:GOTO 1340
1560 IF B=7 THEN SC(A,B)=2
1570 MO=MO+1:LOCATE PX,0:PRINT "M.";MO;:GOSUB 1760:GOSUB 1800:GOTO 380
1580 GOTO 1920
1590 END
1596 REM
1597 REM *** CONTROLLA INPUT GIOCATORE (DA?) ***
1598 REM
1600 GOSUB 2500:H=72-ASC(MID$(A$,1)):E=VAL(MID$(A$,2))-1
1610 IF H<0 OR H>7 OR E<0 OR E>7 OR H=-24 THEN 1640
1620 IF (-1)^(E+H)>0 THEN 1640
1630 WQ=0:RETURN
1640 WQ=1:RETURN
1650 IN=IS-96*H+3*E+706
1660 IF SC(E,H)=1 THEN 1680
1670 IF VPEEK(IN+99)=SP OR VPEEK(IN+93)=SP THEN RETURN
1680 IF VPEEK(IN-99)=SP OR VPEEK(IN-93)=SP THEN RETURN
1690 WQ=1:RETURN
1696 REM
1697 REM *** CONTROLLA INPUT GIOCATORE (A?) ***
1698 REM
1700 GOSUB 2500:B=72-ASC(MID$(A$,1)):A=VAL(MID$(A$,2))-1
1710 IF B<0 OR B>7 OR A<0 OR A>7 OR B=-24 THEN WQ=1:RETURN
1720 IF (-1)^(B+A)<0 THEN RETURN
1730 WQ=1:RETURN
1740 C$=CHR$(72-R(2))+CHR$(R(1)+49):RETURN
1750 C$=CHR$(72-R(4))+CHR$(R(3)+49):RETURN
1754 REM
1755 REM *** POSIZIONA LE PEDINE ***
1756 REM
1760 FOR Y=7 TO 0 STEP -1:FOR X=0 TO 7:IN=IS-96*Y+3*X+706
1770 IF (-1)^(X+Y)>0 THEN 1790
1780 VPOKEIN,TC(SC(X,Y)+2)
1790 NEXT X,Y:RETURN
1797 REM
1798 REM *** PULISCE LE SCRITTE LATERALI ***
1799 REM
1800 PC=0:PT=0:FOR J=0 TO 7:FOR K=0 TO 7:S=SC(J,K):IF S=0 THEN 1830
1810 IF S<0 THEN PC=PC+2*ABS(S)-1:GOTO 1830
1820 PT=PT+2*ABS(S)-1
1830 NEXT:NEXT
1840 FOR I=1 TO 22
1850 LOCATE PX,I
1860 PRINT SPC(32-PX);:NEXT
1870 LOCATE PX,22:PRINT "C"+STR$(PC);:LOCATE PX,23:PRINT "G"+STR$(PT);
1880 REM FOR I=6 TO 22:FOR J=28 TO 39:VPOKE IS+32*I+J,96:NEXT:NEXT
1890 IF PC/PT<=RV THEN 1920
1900 IF PT/PC<=RV THEN 1930
1910 RETURN
1920 LOCATE PX,2:PRINT "Hai";:LOCATE PX,3:PRINT "VINTO";:GOTO 1950
1930 LOCATE PX,2:PRINT "La CPU";:LOCATE PX,3:PRINT "VINCE!";:GOTO 1950
1950 PAUSE 300:RUN
1960 IF FC(18)=0 THEN 2090
1970 IF MC=1 AND CC(18)=1 OR CC(10)=1 OR CC(17)=1 THEN Q=0:MC=0
1980 IF FC(16)=0 THEN 2000
1990 IF CC(12)=2 AND CC(16)<>0 AND CC(18)<>2 THEN Q=Q+60
2000 IF CC(12)=2 AND CC(18) THEN Q=Q+60
2010 IF FC(0)=0 THEN 2030
2020 IF CC(4)=2 AND CC(0)<>0 AND CC(18)<>2 THEN Q=Q+60
2030 IF CC(4)=2 AND CC(18)<>2 THEN Q=Q+60
2040 IF CC(18)=2 OR (CC(17)=2 AND CC(10)=0) OR (CC(17)=0 AND CC(10)=2) THEN 2045
2042 GOTO 2050
2045 Q=-60:RETURN
2050 IF FC(17)=0 THEN 2070
2060 IF CC(18)=1 AND CC(22)=0 AND CC(27)<>2 AND CC(19)<>2 AND CC(26)<>2 THEN Q=Q+25
2070 IF FC(22)=0 THEN 2090
2080 IF CC(18)=1 AND CC(22)=0 AND FC(27)=0 THEN Q=Q+25
2090 IF FC(24)=0 THEN 2110
2100 IF CC(17)=1 AND CC(20)=0 AND CC(16)<>2 AND CC(24)<>2 AND CC(25)<>2 THEN Q=Q+25
2110 IF FC(20)=0 THEN 2130
2120 IF CC(17)=1 AND CC(20)=0 AND FC(24)=0 THEN Q=Q+25
2130 IF FC(3)=0 THEN 2150
2140 IF CC(10)=1 AND CC(6)=0 AND CC(2)<>2 AND CC(3)<>2 AND CC(11)<>2 THEN Q=Q+25
2150 IF FC(6)=0 THEN 2170
2160 IF CC(10)=1 AND CC(6)=0 AND FC(3)=0 THEN Q=Q+25
2170 IF FC(20)=0 OR FC(6)=0 THEN 2190
2180 IF CC(17)>0 AND CC(10)>0 AND CC(6)=0 AND CC(20)=0 THEN Q=Q+35
2190 IF FC(22)=0 THEN 2210
2200 IF CC(18)<0 AND CC(22)=2 THEN Q=Q+25
2210 IF FC(20)=0 THEN 2230
2220 IF CC(17)<0 AND CC(20)>0 THEN Q=Q+25
2230 IF FC(6)=0 THEN 2250
2240 IF CC(10)<0 AND CC(6)=2 THEN Q=Q+15
2250 Q=Q-5:RETURN
2260 M=0:FOR J=-2 TO 4:L=J-2*INT((J+2)/2):FOR K=L TO L+6 STEP 2
2270 UU=U:VV=V
2280 IF MA=1 THEN UU=(X+U)/2:VV=(Y+V)/2
2290 X1=J*(UU-X)+X:Y1=K*(VV-Y)+Y
2300 IF X1<0 OR Y1<0 OR X1>7 OR Y1>7 THEN FC(M)=0:GOTO 2320
2310 CC(M)=SC(X1,Y1):FC(M)=1
2320 M=M+1:NEXT:NEXT:RETURN
2497 REM
2498 REM *** TRASFORMA INPUT IN MAIUSCOLO ***
2499 REM
2500 IF A$="" THEN RETURN
2510 A9$="":FOR I=1 TO LEN(A$)
2520 A9=ASC(MID$(A$,I,1))
2530 IF A9>96 AND A9<123 THEN A9=A9-32
2540 A9$=A9$+CHR$(A9)
2550 NEXT
2560 A$=A9$:RETURN
7997 REM
7998 REM *** INIZIALIZZA L'AREA GRAFICA ***
7999 REM
8000 VOLUME 0,15:SCREEN 1:VREG 7,34:FOR I=0 TO 31:VPOKE 8192+I,242:NEXT
8010 RESTORE 9030:AD=1280:N=4:GOSUB 8500:REM LOAD WHITE CHARS
8020 AD=1536:N=2:GOSUB 8500:REM LOAD BLACK CHARS
8030 VPOKE 8212,&HF6:VPOKE 8216,&H16
8040 SP=160:REM CAMBIARE QUI IL CODICE DEL QUADRO SCURO
8050 RETURN
8500 REM
8510 REM *** CARICA I CARATTERI NELLA VRAM ***
8520 REM
8530 FOR J=0 TO (N-1):FOR I=0 TO 7:READ X:VPOKE AD+(J*8)+I,X:NEXT:NEXT
8540 RETURN
9000 REM
9010 REM *** DATI DEL PROGRAMMA ***
9020 REM
9030 DATA 0,0,0,0,0,0,0,0:REM 160->QUADRO SCURO
9040 DATA 255,255,255,255,255,255,255,255:REM 161->QUADRO CHIARO
9050 DATA 60,126,255,255,255,255,126,60:REM 162->DAMA BIANCA
9060 DATA 0,24,60,126,126,60,24,0:REM 163->PEDINA BIANCA
9070 DATA 60,126,255,255,255,255,126,60:REM 192->DAMA NERA
9080 DATA 0,24,60,126,126,60,24,0:REM 193->PEDINA NERA
10000 REM
10010 REM *** MESSAGGIO DI BENVENUTO E REGOLE ***
10020 REM
10020 CLS:LOCATE 12,2:PRINT "D A M A":PRINT
10030 PRINT "Il gioco segue le regole della"
10040 PRINT "DAMA ITALIANA. Sei obbligato a"
10050 PRINT "mangiare. La partita finisce o"
10060 PRINT "per sconfitta tua o della CPU"
10070 PRINT "oppure per stallo. Il bianco"
10080 PRINT "muvo per primo."
10090 PRINT:PRINT "Premi un tasto":A=INKEY(0)
10100 A=INKEY(10):IF A=0 THEN 10100
10110 RETURN
