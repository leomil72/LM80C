10 REM PROGRAM NAME - OREGON VERSION: 01/01/78
20 REM ORIGINAL PROGRAMMING BY BILL HEINEMANN - 1971
30 REM SUPPORT RESEARCH AND MATERIALS BY DEN RAVITSCH.
40 REM MINNESOTA EDUCATIONAL COMPUTING CONSORTIUM (MECC) STAFF
50 REM CDC CYBER 70/73-26 BASIC 3.1
60 REM DOCUMENTATION BOOKLET 'OREGON' AVAILABLE FROM
70 REM MECC SUPPORT SERVICES - 2520 BROADWAY DRIVE - ST. PAUL, MN 55113
80 REM 
90 REM PORTING TO LM80C BASIC BY LEONARDO MILIANI - 2020
100 REM
110 REM ***FOR THE MEANING OF THE VARIABLES USED, LIST LINES 6470-6790***
120 GOSUB 7000:SCREEN 0:COLOR11,1:DIM S$(5):REM RETURN C$
130 XX=TMR(0):IF XX>0 THEN XX=-XX
140 ZZ=RND(XX):REM RANDOMIZE RNG
200 IF C$="N" THEN 690
210 CLS:LOCATE 12,2
220 PRINT "THE OREGON TRAIL":PRINT:REM *** INSTRUCTIONS ***
230 PRINT "This program simulates a trip over the"
240 PRINT "OREGON TRAIL from Indipendence, Missou-"
250 PRINT "ri, to Oregon City, Oregon, in 1847."
260 PRINT "Your family of five will cover the 2040"
270 PRINT "mile Oregon Trail in 5-6 months. If you"
280 PRINT "make it alive...":PRINT
290 PRINT "You had saved $900 to spend for the"
300 PRINT "trip, and you've just paid $200 for a"
310 PRINT "wagon. You will need to spend the rest"
320 PRINT "of your money on the following items:"
330 SC=3:GOSUB 7500:CLS:PRINT "ITEMS YOU CAN COLLECT:":PRINT
340 PRINT "OXEN:"
350 PRINT "you can spend $200/$300 on your team."
360 PRINT "The more you spend, the fastest you'll"
370 PRINT "go because you'll have better animals.":PRINT
380 PRINT "FOOD:"
390 PRINT "the more you have, the less chance there";
400 PRINT "is of getting sick.":PRINT
405 PRINT "AMMUNITION:"
410 PRINT "$1 buys a belt of 50 bullets. You will"
420 PRINT "need nullets for attacks by animals and"
430 PRINT "bandits. And for huntig food...":PRINT
440 GOSUB 7500:CLS
450 PRINT "CLOTHING:"
460 PRINT "This is especially important for the"
470 PRINT "cold weather you will encounter when"
480 PRINT "crossing the mountains.":PRINT
490 PRINT "MISCELLANEOUS SUPPLIES:"
500 PRINT "This includes medicines and other things";
510 PRINT "you will need for sickness and emergency";
520 PRINT "repairs.":PRINT
530 PRINT
540 PRINT "You can spend all your money before you"
545 PRINT "start your trip - or you can save some"
550 PRINT "of your cash to spend at forts along the";
555 PRINT "way when you run slow. However, items"
560 PRINT "cost more at the forts. You can also go"
565 PRINT "hunting along the way to get more food."
570 GOSUB 7500:CLS
580 PRINT "Whenever you have to use your trusty"
590 PRINT "rifle along the way, you will be told to";
600 PRINT "type in a word (one that sounds like a"
605 PRINT "gun shot). The faster you type in that"
610 PRINT "word and hit the 'RETURN' key, the bet-"
620 PRINT "terluck you'll have with your gun."
630 PRINT:PRINT
640 PRINT "At each turn, all items are shown in"
645 PRINT "dollar amounts except bullets.":PRINT
650 PRINT "When asked to enter money amounts, do"
660 PRINT "NOT use the <$> symbol."
670 PRINT:PRINT "GOOD LUCK !!!"
680 GOSUB 7500
690 CLS
700 PRINT "How good a shot are you with you rifle?"
710 PRINT "(1) Ace Marksman":PRINT"(2) Good Shot"
720 PRINT "(3) Fair to Middlin":PRINT"(4) Need more practice"
730 PRINT "(5) Shaky knees":PRINT:PRINT "Enter one of the above -- ";
740 PRINT "the better you";:PRINT "claim you are, the faster you'll ";
750 PRINT "have":PRINT"to be with your gun to be successful.":PRINT
760 PRINT "Make your choice... ";:D9=INKEY(10)
770 D9=INKEY(50):IF D9<48 OR D9>53 THEN 770
780 PRINT CHR$(D9);:D9=D9-48 
790 IF D9=0 THEN PRINT " ...Wow!"
795 PAUSE 50
800 REM ***INITIAL PURCHASES***
810 X1=1
820 K8=0:S4=0:F1=0:F2=0:M=0:M9=0:D3=0
830 CLS:PRINT "SETTING UP THE TRAIL"
840 LOCATE 0,2:PRINT "How much do you want to spend on your"
850 LOCATE 0,3:PRINT "oxen team:":LOCATE 11,3:INPUT A$:A=VAL(A$)
870 IF A>=200 THEN 900
880 LOCATE 11,3:PRINT "Not enough":PAUSE 50:LOCATE 11,3:PRINTSPC(10)
890 GOTO 850
900 IF A<=300 THEN 930
910 LOCATE 11,3:PRINT "Too much":PAUSE 50:LOCATE 11,3:PRINTSPC(10)
920 GOTO 850
930 LOCATE 0,5:PRINT "How much do you want to spend on food:"
940 LOCATE 0,6:INPUT F$:F=VAL(F$)
950 IF F>=0 THEN 980
960 LOCATE 0,6:PRINT "Impossible":PAUSE 50:LOCATE 0,6:PRINTSPC(10)
970 GOTO 930
980 LOCATE 0,8:PRINT "How much do you want to spend on ammu-"
990 LOCATE 0,9:PRINT "nition:":LOCATE 8,9:INPUT B$:B=VAL(B$)
1000 IF B>=0 THEN 1030
1010 LOCATE 8,9:PRINT "Impossible":PAUSE 50:LOCATE 8,9:PRINTSPC(10)
1020 GOTO 980
1030 LOCATE 0,11:PRINT "How much do you wanto to spend on clo-"
1040 LOCATE 0,12:PRINT "thing:":LOCATE 7,12:INPUT C$:C=VAL(C$)
1050 IF C>=0 THEN 1080
1060 LOCATE 7,12:PRINT "Impossible":PAUSE 50:LOCATE 7,12:PRINTSPC(10)
1070 GOTO 1030
1080 LOCATE 0,14:PRINT "How much do you want to spend on mis-"
1090 LOCATE 0,15:PRINT "scellaneous supplies:":LOCATE 22,15:INPUT M1$:M1=VAL(M1$)
1100 IF M1>=0 THEN 1130
1110 LOCATE 22,15:PRINT "Impossible":PAUSE 50:LOCATE 22,15:PRINTSPC(10)
1120 GOTO 1080
1130 T=700-A-F-B-C-M1:PRINT
1140 IF T>=0 THEN 1170
1150 PRINT "You overspent -- You only had $700 to"
1155 PRINT "spend. Buy again":PAUSE 200
1160 GOTO 830
1170 B=50*B
1180 PRINT "After all purchases, you now have ";T:PRINT "dollars left.":PAUSE 200
1190 CLS
1200 PRINT "Monday, March 29, 1847"
1210 PRINT
1220 GOTO 1750
1230 IF M>=2040 THEN 5430
1240 REM ***SETTING DATE***
1250 D3=D3+1
1260 PRINT
1270 PRINT "Monday, ";
1280 IF D3>10 THEN 1300
1290 ON D3 GOTO 1310,1330,1350,1370,1390,1410,1430,1450,1470,1490
1300 ON D3-10 GOTO 1510,1530,1550,1570,1590,1610,1630,1650,1670,1690
1310 PRINT "April 12, ";
1320 GOTO 1720
1330 PRINT "April 26, ";
1340 GOTO 1720
1350 PRINT "May 10, ";
1360 GOTO 1720
1370 PRINT "May 24, ";
1380 GOTO 1720
1390 PRINT "June 7, ";
1400 GOTO 1720
1410 PRINT "June 21, ";
1420 GOTO 1720
1430 PRINT "July 5, ";
1440 GOTO 1720
1450 PRINT "July 19, ";
1460 GOTO 1720
1470 PRINT "August 2, ";
1480 GOTO 1720
1490 PRINT "August 16 ";
1500 GOTO 1720
1510 PRINT "August 31, ";
1520 GOTO 1720
1530 PRINT "September 13, ";
1540 GOTO 1720
1550 PRINT "September 27, ";
1560 GOTO 1720
1570 PRINT "September 11, ";
1580 GOTO 1720
1590 PRINT "September 25, ";
1600 GOTO 1720
1610 PRINT "September 8, ";
1620 GOTO 1720
1630 PRINT "September 22, ";
1640 GOTO 1720
1650 PRINT "September 6, ";
1660 GOTO 1720
1670 PRINT "September 20, ";
1680 GOTO 1720
1690 PRINT "You have been on the trail too long ---"
1700 PRINT "Your family dies in the first blizzard":PRINT"of Winter"
1710 GOTO 5170
1720 PRINT "1847"
1730 PRINT
1740 REM ***BEGINNING EACH TURN***
1750 IF F>=0 THEN 1770
1760 F=0
1770 IF B>=0 THEN 1790
1780 B=0
1790 IF C>=0 THEN 1810
1800 C=0
1810 IF M1>=0 THEN 1830
1820 M1=0
1830 IF F>=13 THEN 1850
1840 PRINT "You'd better do some hunting or buy food";:PRINT"and soon!!!"
1850 F=INT(F)
1860 B=INT(B)
1870 C=INT(C)
1880 M1=INT(M1)
1890 T=INT(T)
1900 M=INT(M)
1910 M2=M
1920 IF S4=1 THEN 1950
1930 IF K8=1 THEN 1950
1940 GOTO 1990
1950 T=T-20
1960 IF T<0 THEN 5080
1970 PRINT "Doctor's bill is $20"
1980 K8=S4=0
1990 IF M9=1 THEN 2020
2000 PRINT "Total mileage is";M
2010 GOTO 2040
2020 PRINT "Total mileage is 950"
2030 M9=0
2040 PRINT "Food  Bullets Clothing Misc.Supp. Cash"
2050 PRINT F;TAB(7);B;TAB(15);C;TAB(24);M1;TAB(34);T
2060 PRINT:IF X1=-1 THEN 2170
2070 X1=X1*(-1)
2080 PRINT "Do you want to (1) stop at the next"
2090 PRINT"fort, (2) hunt, or (3) continue"
2100 INPUT X
2110 IF X>2 THEN 2150
2120 IF X<1 THEN 2150
2130 X=INT(X)
2140 GOTO 2270
2150 X=3
2160 GOTO 2270
2170 PRINT "Do you want to (1) hunt, or (2) continue";
2180 INPUT X
2190 IF X=1 THEN 2210
2200 X=2
2210 X=X+1
2220 IF X=3 THEN 2260
2230 IF B>39 THEN 2260
2240 PRINT "Tough --- You need more bullets to go":PRINT"hunting"
2250 GOTO 2170
2260 X1=X1*(-1)
2270 ON X GOTO 2290, 2540, 2720
2280 REM ***STOPPING AT FORT***
2290 PRINT "Enter what you wish to spend on the fol-";:PRINT"lowing"
2300 PRINT "Food";
2310 GOSUB 2330
2320 GOTO 2410
2330 INPUT P
2340 IF P<0 THEN 2400
2350 T=T-P
2360 IF T>=0 THEN 2400
2370 PRINT "You don't have that much.":PRINT "Keep your spending down"
2375 PRINT "You miss your chance to spend on that":PRINT"item"
2380 T=T+P
2390 P=0
2400 RETURN
2410 F=F+2/3*P
2420 PRINT "Ammunition";
2430 GOSUB 2330
2440 B=INT(B+2/3*P*50)
2450 PRINT "Clothing";
2460 GOSUB 2330
2470 C=C+2/3*P
2480 PRINT "Misc. supplies";
2490 GOSUB 2330
2500 M1=M1+2/3*P
2510 M=M-45
2520 GOTO 2720
2530 REM ***HUNTING***
2540 IF B>39 THEN 2570
2550 PRINT "Tough --- You need more bullets to go":PRINT"hunting"
2560 GOTO 2080
2570 M=M-45
2580 GOSUB 6140
2590 IF B1<=1 THEN 2660
2600 IF 100*RND(1)<13*B1 THEN 2710
2610 F=F+48-2*B1
2620 PRINT "Nice shot! Right on target!":PRINT"Good eatin' tonight!!"
2630 B=B-10-3*B1
2640 GOTO 2720
2650 REM **BELLS IN LINE 2660**
2660 PRINT "Right between the eyes!":PRINT"You got a big one!!!!"
2670 PRINT "Full bellies tonight!"
2680 F=F+52*RND(1)*6
2690 B=B-10-RND(1)*4
2700 GOTO 2720
2710 PRINT "You missed... And your dinner got away..";
2720 IF F>=13 THEN 2750
2730 GOTO 5060
2740 REM ***EATING***
2750 PRINT "Do you want to eat (1) poorly,"
2760 PRINT "(2) moderately, or (3) well";
2770 INPUT E
2780 IF E>3 THEN 2750
2790 IF E<1 THEN 2750
2800 E=INT(E)
2810 F=F-8-5*E
2820 IF F>=0 THEN 2860
2830 F=F*S+5*E
2840 PRINT "You can't eat that well"
2850 GOTO 2750
2860 M=M+200+(A-220)/5+10*RND(1)
2870 L1=0:C1=0
2880 REM ***RIDERS ATTACK***
2890 IF RND(1)*10>((M/100-4)^2+72)/((M/100-4)^2+12)-1 THEN 3550
2900 PRINT "Riders ahead. They ";
2910 S5=0
2920 IF RND(1)<.8 THEN 2950
2930 PRINT "don't "
2940 S5=1
2950 PRINT "look hostile"
2960 PRINT "Tactics"
2970 PRINT "(1) run        (2) attack":PRINT "(3) continue   (4) circle wagons"
2980 IF RND(1)>.2 THEN 3000
2990 S5=1-S5
3000 INPUT T1
3010 IF T1<1 THEN 2970
3020 IF T1>4 THEN 2970
3030 T1=INT(T1)
3040 IF S5=1 THEN 3330
3050 IF T1>1 THEN 3110
3060 M=M+20
3070 M1=M1-IS
3080 B=B-150
3090 A=A-40
3100 GOTO 3470
3110 IF T1>2 THEN 3240
3120 GOSUB 6140:B1=B1-1
3130 B=B-B1*40-80
3140 IF B1>1 THEN 3170
3150 PRINT "Nice shooting. You drove them off"
3160 GOTO 3470
3170 IF B1<=4 THEN 3220
3180 PRINT "Lousy shot. You got knifed"
3190 K8=1
3200 PRINT "You have to see ol' Doc Blanchard"
3210 GOTO 3470
3220 PRINT "Kinda slow with your colt .45"
3230 GOTO 3470
3240 IF T1>3 THEN 3290
3250 IF RND(1)>.8 THEN 3450
3260 B=B-150
3270 M1=M1-15
3260 GOTO 3470
3290 GOSUB 6140
3300 B=B-B1*30-80
3310 M=M-25
3320 GOTO 3140
3330 IF T1>1 THEN 3370
3340 M=M+15
3350 A=A-10
3360 GOTO 3470
3370 IF T1>2 THEN 3410
3380 M=M-5
3390 B=B-100
3400 GOTO 3470
3410 IF T1>3 THEN 3430
3420 GOTO 3470
3430 M=M-20
3420 GOTO 3470
3450 PRINT "They did not attack"
3460 GOTO 3550
3470 IF S5=0 THEN 3500
3480 PRINT "Riders were friendly, but check for pos-";:PRINT"sible losses"
3490 GOTO 3550
3500 PRINT "Riders were hostile -- Check for losses"
3510 IF B>=O THEN 3550
3520 PRINT "You ran out of bullets and got massacred":PRINT"by the riders"
3530 GOTO 5170
3540 REM ***SELECTION OF EVENTS***
3550 D1=0
3560 RESTORE
3570 R1=100*RND(1)
3580 D1=D1+1
3590 IF D1=16 THEN 4670
3600 READ D
3610 IF R1>D THEN 3580
3620 DATA 6,11,13,15,17,22,32,35,37,42,44,54,64,69,95
3630 IF D1>10 THEN 3650
3640 ON D1 GOTO 3660,3700,3740,3790,3820,3850,3880,3960,4130,4190
3650 ON D1-10 GOTO 4220,4290,4340,4560, 4610,4670
3660 PRINT "Wagon breaks down. Lose time and sup-":PRINT"plies fixing it"
3670 M=M-15-5*RND(1)
3680 M1=M1-8
3690 GOTO 4710
3700 PRINT "Ox injuries leg. Slows you down rest of":PRINT"trip"
3710 M=M-25
3720 A=A-20
3730 GOTO 4710
3740 PRINT "Bad luck. Your daughter broke her arm"
3750 PRINT "You had to stop and use supplies to":PRINT"make a sling"
3760 M=M-5-4*RND(1)
3770 M1=M1-2-3*RND(1)
3780 GOTO 4710
3790 PRINT "Ox wanders off... Spend time looking for";:PRINT"it"
3800 M=M-17
3810 GOTO 4710
3820 PRINT "Your son gets lost. Spend half the day":PRINT"looking for him"
3830 M=M-10
3840 GOTO 4710
3850 PRINT "Unsafe water. Lose time looking for":PRINT"clean spring"
3860 M=M-10*RND(1)-2
3870 GOTO 4710
3880 IF M>950 THEN 4490
3890 PRINT "Heavy rains. Time and supplies lost"
3910 F=F-10
3920 B=B-500
3930 M1=M1-15
3940 M=M-10*RND(1)-5
3950 GOTO 4710
3960 PRINT "Bandits attack"
3970 GOSUB 6140
3980 B=B-20*B1
3990 IF B>O THEN 4030
4000 PRINT "You ran out of bullets. They get lots":PRINT"of cash"
4010 T=T/3
4020 GOTO 4040
4030 IF B1<=1 THEN 4100
4040 PRINT "You got sobt in the leg and they took":PRINT"one of your oxen"
4050 K8=1
4060 PRINT "Better have a doc look at your wound"
4070 M1=M1-5
4080 A=A-20
4090 GOTO 4710
4100 PRINT "Quickest draw outside of Dodge City!!"
4110 PRINT "You got 'em!!"
4120 GOTO 4710
4130 PRINT "There was a fire in you wagon! Food and":PRINT"supplies damaged!"
4140 F=F-40
4150 B=B-400
4160 M1=M1-RND(1)*5-3
4170 M=M-15
4180 GOTO 4710
4190 PRINT "Lose your way in heavy fog. Time is lost";
4200 M=M-10-5*RND(1)
4210 GOTO 4710
4220 PRINT "You killed a poisonous snake after it":PRINT"bit you"
4230 B=B-10
4240 M1=M1-5
4250 IF M1>=0 THEN 4280
4260 PRINT "You die of snakebite since you have no":PRINT"medicine"
4270 GOTO 5170
4280 GOTO 4710
4290 PRINT "Wagon gets swamped fording river. Lose":PRINT"food and clothes"
4300 F=F-30
4310 C=C-20
4320 M=M-20-20*RND(1)
4330 GOTO 4710
4340 PRINT "Wild animals attack!"
4350 GOSUB 6140
4360 IF B>39 THEN 4410
4370 PRINT "You were too low on bullets.."
4380 PRINT "The wolves overpowered you"
4390 K8=1
4400 GOTO 5120
4410 IF B1>2 THEN 4440
4420 PRINT "Nice shootin' pardner. They didn't get":PRINT"much"
4430 GOTO 4450
4440 PRINT "Slow on the draw. They got at your":PRINT"food and clothes"
4450 B=B-20*B1
4460 C=C-B1*4
4470 F=F-B1*5
4480 GOTO 4710
4490 PRINT "Cold weather.... Brrrrrrrr.... You ";
4500 IF C>22+4*RND(1) THEN 4530
4510 PRINT "don't ";
4520 C1=1
4530 PRINT "have enough clothing to keep you warm"
4540 IF C1=0 THEN 4710
4550 GOTO 6300
4560 PRINT "Hail storm... Supplies damaged"
4570 M=M-5*RND(1)*10
4580 B=B-200
4590 M1=M1-4-RND(1)*3
4600 GOTO 4710
4610 IF E=1 THEN 6300
4620 IF E=3 THEN 4650
4630 IF RND(1)>.25 THEN 6300
4640 GOTO 4710
4650 IF RND(1)<.5 THEN 6300
4660 GOTO 4710
4670 PRINT "Helpful indians show you where to find":PRINT"more food"
4680 F=F+14
4690 GOTO 4710
4700 REM ***MOUNTAINS***
4710 IF M <= 950 THEN 1230
4720 IF RND(1)*10>9-((M/100-15)^2+72)/((M/100-15)^2+12) THEN 4860
4730 PRINT "Rugged Mountains"
4740 IF RND(1)>.1 THEN 4780
4750 PRINT "You got lost. Lose valuable time trying":PRINT"to find trail!"
4760 M=M-60
4770 GOTO 4860
4780 IF RND(1)>.11 THEN 4840
4790 PRINT "Wagon damaged! Lose time and supplies"
4800 M1=M1-5
4810 B=B-200
4820 M=M-20-30*RND(1)
4830 GOTO 4860
4840 PRINT "The going gets slow"
4850 M=M-45-RND(1)/.02
4860 IF F1=1 THEN 4900
4870 F1=1
4880 IF RND(1)<.8 THEN 4970
4890 PRINT "You made it safely through South Pass -":PRINT"no snow"
4900 IF M<1700 THEN 4940
4910 IF F2=1 THEN 4940
4920 F2=1
4930 IF RND(1)<.7 THEN 4970
4940 IF M>950 THEN 1230
4950 M9=1
4960 GOTO 1230
4970 PRINT "Blizzard in mountain pass... Time and":PRINT"supplies lost"
4980 L1=1
4990 F=F-25
5000 M1=M1-10
5010 B=B-300
5020 M=M-30-40*RND(1)
5030 IF C<18+2*RND(1) THEN 6300
5040 GOTO 4940
5050 REM ***DYING***
5060 PRINT "You ran out of food and starved to death";
5070 GOTO 5170
5080 T=0
5090 PRINT "You can't afford a doctor"
5100 GOTO 5120
5110 PRINT "You ran out of medical supplies"
5120 PRINT "You died of ";
5130 IF K8=1 THEN 5160
5140 PRINT "pneumonia"
5150 GOTO 5170
5160 PRINT "injuries"
5170 PRINT
5180 PRINT "Due to your unfortunate situation, there";
5190 PRINT "are a few formalities we must go through";
5200 PRINT
5210 PRINT "Would you like a Minister?"
5220 INPUT C$
5230 PRINT "Would you like a fancy funeral?"
5240 INPUT C$
5250 PRINT "Would you like us to inform your next":PRINT "of kin?"
5260 INPUT C$:GOSUB 7600
5270 IF C$="YES" OR C$="Y" THEN 5310
5280 PRINT "But your aunt Sadie in St. Louis is":PRINT"really worried about you"
5290 PRINT
5300 GOTO 5330
5310 PRINT "That will be $4.50 for the telegraph":PRINT"charge.."
5320 PRINT
5330 PRINT "We thank you for this information and"
5335 PRINT "we are sorry you didn't make it to the"
5340 PRINT "GREAT TERRITORY OF OREGON"
5350 PRINT "Better luck next time"
5360 PRINT
5370 PRINT
5380 PRINT " Sincerely"
5390 PRINT
5400 PRINT "The Oregon City Chamber of Commerce"
5410 STOP
5420 REM ***FINAL TURN***
5430 F9=(2040-M2)/(M-M2)
5440 F=F+(1-F9)*(8+5*E)
5450 PRINT
5460 REM **BELLS IN LINES 5470, 5480**
5470 PRINT "You finally arrived at Oregon City after";
5480 PRINT "2040 long miles... HOORAY!!!!"
5490 PRINT "A real pioneer!"
5500 PRINT
5510 F9=INT(F9*14)
5520 D3=D3*14+F9
5530 F9=F9+1
5540 IF F9<8 THEN 5560
5550 F9=F9-7
5560 ON F9 GOTO 5570,5590,5610,5630,5650,5670,5690
5570 PRINT "Monday, ";
5580 GOTO 5700
5590 PRINT "Tuesday, ";
5600 GOTO 5700
5610 PRINT "Wednesday, ";
5620 GOTO 5700
5630 PRINT "Thursday, ";
5640 GOTO 5700
5650 PRINT "Friday, ";
5660 GOTO 5700
5670 PRINT "Saturday, ";
5680 GOTO 5700
5690 PRINT "Sunday, ";
5700 IF D3>124 THEN 5740
5710 D3=D3-93
5720 PRINT "July ";D3;", 1847"
5730 GOTO 5920
5740 IF D3>155 THEN 5780
5750 D3=D3-124
5760 PRINT "August ";D3;", 1847"
5770 GOTO 5920
5780 IF D3>185 THEN 5820
5790 D3=D3-155
5800 PRINT "September ";D3;", 1847"
5810 GOTO 5920
5820 IF D3>216 THEN 5860
5830 D3=D3-185
5840 PRINT "October ";D3;", 1847"
5850 GOTO 5920
5860 IF D3>246 THEN 5900
5870 D3=D3-216
5880 PRINT "November ";D3;", 1847"
5890 GOTO 5920
5900 D3=D3-246
5910 PRINT "December ";D3;", 1847"
5920 PRINT
5930 PRINT "Food  Bullets Clothing Misc.Supp. Cash"
5940 IF B>O THEN 5960
5950 B=0
5960 IF C>O THEN 5980
5970 C=0
5980 IF M1>0 THEN 6000
5990 M1=0
6000 IF T>O THEN 6020
6010 T=0
6020 IF F>O THEN 6040
6030 F=0
6040 PRINT INT(F);TAB(7);INT(B);TAB(15);INT(C);TAB(24);INT(M1);TAB(34);INT(T)
6050 PRINT
6060 PRINT " President James K. Polk sends you his"
6070 PRINT "       heartiest congratulations"
6080 PRINT
6090 PRINT "and wishes you a prosperous life ahead"
6100 PRINT
6110 PRINT "           at your new home"
6120 GOSUB 7500:CLS:END
6130 REM ***SHOOTING SUB-ROUTINE***
6131 REM THE METHOD OF TIMING THE SHOOTING (LINES 6210-6240)
6132 REM WILL VARY FROM SYSTEM TO SYSTEM. FOR EXAMPLE, H-P
6133 REM USERS WILL PROBABLY PREFER TO USE THE 'ENTER' STATEMENT.
6134 REM IF TIMING ON THE USER'S SYSTEM IS HIGHLY SUSCEPTIBLE
6135 REM TO SYSTEM RESPONSE TIME, THE FORMULA IN LINE 6240 CAN
6136 REM BE TAILORED TO ACCOMODATE THIS BY EITHER INCREASING
6137 REM OR DECREASING THE 'SHOOTING' TIME RECORDED BY THE SYSTEM.
6140 S$(1)="BANG"
6160 S$(2)="BLAM"
6170 S$(3)="POW"
6180 S$(4)="WHAM"
6190 S6=INT(RND(1)*4+1)
6200 PRINT "Type ";S$(S6)
6210 B3=TMR(0):INPUT C$
6220 B1=TMR(0):GOSUB 7600
6230 B1=((B1-B3)/100)-(D9-1)+1
6240 PRINT
6250 IF B1>0 THEN 6260
6255 B1=0
6260 IF C$=S$(S6) THEN 6280
6270 B1=9
6280 RETURN
6290 REM ***ILLNESS SUB-ROUTINE***
6300 IF 100*RND(1)<10+35*(E-1) THEN 6370
6310 IF 100*RND(1)<100-(40/4^(E-1)) THEN 6410
6320 PRINT "Serious illness---"
6330 PRINT "You must stop for medical attention"
6340 M1=M1-10
6350 S4=1
6360 GOTO 6440
6370 PRINT "Mild illness---Medicine used"
6360 M=M-5
6390 M1=M1-2
6400 GOTO 6440
6410 PRINT "Bad illness---Medicine used"
6420 M=M-5
6430 M1=M1-5
6440 IF M1<0 THEN 5110
6450 IF L1=1 THEN 4940
6460 GOTO 4710
6470 REM ***IDENDIFICATION OF VARIABLES IN THE PROGRAM***
6480 REM A  = AMOUNT SPENT ON ANIMALS
6490 REM B  = AMOUNT SPENT ON AMMUNITION
6500 REM B1 = ACTUAL RESPONSE TIME FOR INPUTTING "BANG"
6510 REM B3 = CLOCK TIME AT START OF INPUTTING "BANG"
6520 REM C  = AMOUNT SPENT ON CLOTHING
6530 REM C1 = FLAG FOR INSUFFICIENT CLOTHING IN COLD WEATHER
6540 REM C$ = YES/NO RESPONSE TO QUESTIONS
6550 REM D1 = COUNTER IN GENERATING EVENTS
6560 REM D3 = TURN NUMBER FOR SETTING DATE
6570 REM D4 = CURRENT DATE
6580 REM D9 = CHOICE OF SHOOTING EXPERTISE LEVEL
6590 REM E  = CHOICE OF EATING
6600 REM F  = AMOUNT SPENT ON FOOD
6610 REM F1 = FLAG FOR CLEARING SOUTH PASS
6620 REM F2 = FLAG FOR CLEARING BLUE MOUNTAINS
6630 REM F9 = FRACTION OF 2 WEEKS TRAVELED ON FINAL TURN
6640 REM K8 = FLAG FOR INJURY
6650 REM L1 = FLAG FOR BLIZZARD
6660 REM M  = TOTAL MILEAGE WHOLE TRIP
6670 REM M1 = AMOUNT SPENT ON MISCELLANEOUS SUPPLIES
6680 REM M2 = TOTAL MILEAGE UP THROUGH PREVIOUS TURN
6690 REM M9 = FLAG FOR CLEARING SOUTH PASS IN SETTING MILEAGE
6700 REM P  = AMOUNT SPENT ON ITEMS AT FORT
6710 REM R1 = RANDOM NUMBER IN CHOOSING EVENTS
6720 REM S4 = FLAG FOR ILLNESS
6730 REM S5 = HOSTILITY OF RIDERS FACTOR
6740 REM S6 = SHOOTING WORD SELECTOR
6750 REM S$ = VARIATIONS OF SHOOTING WORD
6760 REM T  = CASH LEFT OVER AFTER INITIAL PURCHASES
6770 REM T1 = CHOICE OF TACTICS WHEN ATTACKED
6780 REM X  = CHOICE OF ACTION FOR EACH TURN
6790 REM XI = FLAG FOR FORT OPTION
6800 END
6990 REM *** SPLASH SCREEN ***
7000 SCREEN 1:COLOR11,1,1:RESTORE 7200
7010 YY=1:XX=5:FOR XI=1 TO 4
7020 LOCATE XX,YY:FOR XJ=1 TO 21
7030 READ CH:PRINT CHR$(CH);:NEXT
7040 YY=YY+1:NEXT:LOCATE 21,YY:PRINT CHR$(137);
7050 YY=YY+2:XX=11:FOR XI=1 TO 4
7060 LOCATE XX,YY:FOR XJ=1 TO 9
7070 READ CH:PRINT CHR$(CH);:NEXT
7080 YY=YY+1:NEXT:YY=YY+3
7090 LOCATE 3,YY:PRINT"Ported by Leonardo Miliani":YY=YY+1
7100 LOCATE 0,YY:PRINT"Original code by Bill Heinamann":YY=YY+2
7110 LOCATE 0,18:PRINT "Do you need instructions (Y/N)?":ZZ=INKEY(5)
7120 ZZ=INKEY(50):IF ZZ=0 THEN 7120
7130 ZZ=ZZ AND 223:IF ZZ=89 THEN C$="Y":GOTO 7160
7140 IF ZZ=78 THEN C$="N":GOTO 7160
7150 GOTO 7120
7160 RETURN
7200 DATA 135,141,136,144,32,32,32,32,135,143,136,32,32,32,32,32,32,32,32,32,32
7210 DATA 32,144,32,140,136,135,136,32,144,32,144,135,136,135,136,135,136,135,136
7220 DATA 135,136
7230 DATA 32,144,32,144,144,140,137,32,144,32,144,140,137,140,137,138,142,144,144
7240 DATA 144,144
7250 DATA 32,139,32,139,138,138,32,32,138,143,137,144,146,138,32,32,144,138,137
7260 DATA 139,138
7270 DATA 135,141,136,32,32,32,32,46,144
7280 DATA 32,144,32,135,136,32,136,144,144
7290 DATA 32,144,32,140,137,135,142,144,144
7300 DATA 32,139,32,144,146,138,137,139,138
7490 REM *** WAIT FOR A KEY ***
7500 PRINT:PRINT SPC(4+SC);"Press any key to continue..."
7510 ZZ=INKEY(5)
7520 ZZ=INKEY(50):IF ZZ=0 THEN 7520
7530 RETURN
7590 REM **CONVERT ANY TEXT IN C$ IN UPPER CASE**
7600 XX=0:YY=0
7610 TT$="":FOR J=1 TO LEN(C$):TC$=MID$(C$,J,1)
7620 IF TC$>="a" AND TC$<="z" THEN TT$=TT$+CHR$(ASC(TC$)-32):GOTO 7640
7630 TT$=TT$+TC$
7640 NEXT:C$=TT$
7650 RETURN
